buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
    id 'maven-publish'
}

group = 'pl.touk.krush'
project.version = scmVersion.version

sourceCompatibility = 1.8

allprojects {
    repositories {
        jcenter()
    }

    version = "0.3.0.1"
}


configure([project(':annotation-processor'), project(':runtime')]) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                groupId rootProject.group
                artifactId project.name
                version rootProject.version

                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }

        repositories {
            maven {
                def releasesRepoUrl = "$rootProject.buildDir/repos/releases"
                url = releasesRepoUrl
            }
        }
    }
}
